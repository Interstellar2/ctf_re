第一个样例程序——HELLO WORLD

#C语言代码：

#include <stdio.h>

int main()
{
	printf("Hello, world!\n");
	return 0;
}
#使用在线工具的MSVC编译器，我们可以得到下面的汇编指令结果：

...
CONST   SEGMENT
$SG5328 DB        'Hello, world!', 0aH, 00H
CONST   ENDS
...
$SG5328 DB        'Hello, world!', 0aH, 00H
EXTRN   ___acrt_iob_func:PROC
EXTRN   ___stdio_common_vfprintf:PROC
_TEXT   SEGMENT
_main   PROC
        push     ebp
        mov      ebp, esp
        push     OFFSET $SG5328
        call     _printf
        add      esp, 4
        xor      eax, eax
        pop      ebp
        ret      0
_main   ENDP
_TEXT   ENDS

汇编语言存在两种主流语体，即Intel语体和AT&T语体。MSVC生成的汇编清单文件都采用了Intel语体。 在hello world这个例子中，我们可以看到结果中的代码中有COST和_TEXT段。它们分别代表数据段和代码段。C/C++程序为字符串常量"hello, world!"分配了一个指针(cost char[])，只是在代码中这个指针的名称并不明显。然后，编译器自己进行了处理，并在内部把字符串常量命名为$SG5328。 因此，上述程序的源代码等效于：

#include <stdio.h>

const cahr *$SG5328[] = "hello, world!\n";

int main()
{
	printf($SG5328);
	return 0;
}

回顾编译结果，我们发现编译器在字符串常量的尾部添加了十六进制的数字0，即00h。依据C/C++字符串的标准规范，编译器要为这个字符串常量添加结束标志（即数值为零的单个字节）。



#GCC

接下来，我们使用GCC编译器来编译这个hello world程序。（书中的gcc版本为4.4.1），执行命令：gcc 1.c -o 1

然后使用反汇编工具IDA查看main()函数的具体情况。IDA所输出的汇编指令格式，与MSVC生成的汇编指令格式相同，它们都采用Intel语体显示汇编指令。

main		proc near
var_c10		= dword ptr -10h

			push ebp
			push ebp,esp
			and esp,0FFFFFFF0h
			sub esp,10h
			mov eax,offset aHelloWorld	;"hello, world\n"
			mov [esp+10h+var_10],eax
			call _printf
			mov eax,0
			leave
			retn
main		endp

GCC生成的汇编指令，与MSVC生成的结果基本相同。它首先把"hello, world"字符串在数据段的地址（指针）存储到EAX寄存器里，然后再把它存储到数据栈里。 其中值得注意的是还有开场部分的and esp,0fffffff0h指令。它令栈地址（ESP的值）向16字节边界对齐（成为16的整数倍），属于初始化的指令。如果地址没有对齐，那么CPU可能需要访问再次内存才能获得栈内数据。虽然在8字节边界对齐就可以满足32位x86 CPU和64位x64 CPU的要求，但是主流编译器的编译规则规定程序访问的地址必须向16字节对齐（被16整除）。人们还是为了提高指令的执行效率而特意拟定了这条编译规范。

sub esp,10h将在栈中分配0x10 bytes，即16字节。我们在后文看到，程序只会用到4字节空间。但是因为编译器对栈地址（ESP）进行了16字节对齐，所以每次都会分配16字节的空间。

而后，程序将字符串地址（指针的值）直接写入到数据栈。此处，GCC使用的是MOV指令；而MSVC生成的是PUSH指令。其中var_10是局部变量，用来向后面的printf()函数传递参数。

随即，程序调用printf()函数。 GCC和MSVC不同，除非人工指定优化选项，否则它会生成与源代码直接对应的mov eax,0指令。但是，我们已经知道MOV指令的OPCODE肯定要比XOR指令的OPCODE长。

最后一条leave指令，等效于mov esp,ebp和pop ebp两条指令。可见，这个指令调整了数据栈指针ESP，并将EBP的数值恢复到调用这个函数之前的初始状态。毕竟，程序段在开始部分就对EBP和ESP进行了操作，所以函数要在退出之前恢复这些寄存器的值。

#GCC:AT&T语体

AT&T语体同样是汇编语言的显示风格。这种语体在UNIX之中较为常见。

我们使用GCC4.7.3编译源程序并启用GCC编译选项-fno-asynchronous-unwind-tables，将会得到如下指令

.LC0:
		.string "hello, world\n"
main:
		pushl %ebp
		movl %esp,%ebp
		andl $-16,%esp
		subl $16,%esp
		movl $.LC0,(%esp)
		call printf
		movl $0,%eax
		leave
		ret

##Intel语体和AT&T语体的区别

1。运算表达式的书写顺序相反。
        Intel 格式：<指令> <目标> <源>
        AT&T 格式：<指令> <源> <目标> 如果我们将Intel语体的指令认为是等号（=）赋值，那么AT&T语体结构使用的就是右箭头（->)进行赋值。部分C标准函数的运算单元的书写格式确实是相同的，例如memcpy()、strcpy()。
2.AT&T语体中，在寄存器名称之前使用百分号（%）标记，在立即数之前使用美元符号（$）标记。AT&T语体使用圆括号，而Intel语体使用方括号。
3.AT&T语体里，每个运算操作符都需要声明操作数据的类型： -9：quad（64位） -l：指代32位long类型数据 -w：指代16位word类型数据 -b：指代8位byte类型数据

#x64
MSVC-x86-64

若用64位的MSVC编译上述程序，则会得到下述指令：

...
CONST   SEGMENT
$SG5328 DB        'Hello, world!', 0aH, 00H
CONST   ENDS
...
$SG5328 DB        'Hello, world!', 0aH, 00H
EXTRN   ___acrt_iob_func:PROC
EXTRN   ___stdio_common_vfprintf:PROC
_TEXT   SEGMENT
_main   PROC
			sub rsp,40
			lea rcx,OFFSET FLAT:$SG5328
			call printf
			xor eax,eax
			add rsp,40
			ret 0
_main   ENDP
_TEXT   ENDS

#GCC 4.4.6 x64

使用64位的Linux的GCC编译器编译上述程序，可得到如下指令：

.string "hello, world\n"
main:
		sub rsp,8
		mov edi,OFFSET FLAT:.LC0	;"hello, world"
		xor eax,eax		;number of vector registers passed
		call printf
		xor eax,eax
		add rsp,8
		ret

GCC的其他特性

只要C语言代码里使用了字符串常量，编译器就会把这个字符串常量置于常量字段，以保证其内容不会发生变化。不过GCC有个有趣的特征：它可能会把字符串拆出来单独使用。 例如：

#include <stdio.h>
int f1()
{
	printf("world\n");
}

int f2()
{
	printf("hello world\n");
}

int main()
{
	f1();
	f2();
}

多数的C/C++编译器（包括MSVC编译器）会分配出两个直接对应的字符串，不过GCC 4.8.1的编译结果则更为可圈可点：

f1			proc near

s			= dword ptr -1Ch
			
			sub esp,1Ch
			mov [esp+1Ch+s],offset s		;"world\n"
			call _puts
			add esp,1Ch
			retn
f1			endp

f2			proc near

s			= dword ptr -1Ch
			sub esp,1Ch
			mov [esp+1Ch+s],offset aHello		;"hello"
			call _puts
			retn
f2			endp

aHello		db 'hello'
s				db 'world', 0xa, 0

在打印字符串"hello world"的时候，这两个词的指针地址实际上是前后相邻的。在调用puts()函数进行输出时，函数本身不知道它所输出的字符串分为两个部分。实际上我们在汇编指令清单中可以看到，这两个字符串没有被 切实 分开。 在f1()函数调用puts()函数时，它输出字符串“world”和外加结束符（数值为零的1个字节），因为puts()函数并不知道字符串可以和前面的字符连起来形成新的字符串。 GCC编译器会使用这种技术充分节省内存。

#ARM

ARM 程序使用R0寄存器传递函数返回值。 ARM 程序使用LR寄存器(Link Register)存储函数结束之后的返回地址(RA/ Return Address)。x86 程序使用“栈”结构存储上述返回地址。 BX LR指令的作用是跳转到返回地址，即返回到调用者函 数，然后继续执行调用体 caller 的后续指令。
Keil 6/2013——未启用优化功能的ARM模式

可使用下面的命令，用Keil编译器把hello world程序编译为ARM指令集架构的汇编程序： armcc.exe --arm --c90 -O0 1.c 虽然armcc编译器生成的汇编指令清单同样采用了Intel语体，但是程序所使用的宏却很有ARM处理器的特色。

.text:00000000	 					main
.text:00000000	10 40 2D E9	STMFD SP!,{R4,LR}
.text:00000004	1E 0E 8F E2	ADR R0,aHelloWorld	;"hello world"
.text:00000008	15 19 00 EB	BL __2printf
.text:0000000C	00 00 A0 E3	MOV R0,#0
.text:00000010	10 80 BD E8	LDMFD SP!,{R4,PC}

.text:000001EC	68 65 6C 6C	+aHelloWorld DCB "hello, world",0	;DATA XREF:main+4




ARM64
GCC

使用GCC 4.8.1 将上述代码编译为ARM64程序，可得到如下代码：

0000000000400590 <main>:
	400590:		a9bf7bfd	stp	x29, x30, [sp,#-16]!
	400594:		910003fd	mov	x29, sp
	400598:		90000000	adrp	x0,400000 <_init-0x3b8>
	40059c:		91192000	add	x0, x0, #0x648
	4005a0:		97ffffa0	bl	400420 <puts@plt>
	4005a4:		52800000	mov	w0, #0x0 // #0
	4005a8:		a8c17bfd	ldp x29, x30, [sp],#16
	4005ac:		d65f03c0	ret
...
Contents of section .rodata:
400640 01000200 00000000 48656c6c 6f210000 ........Hello!..

一方面，ARM64的CPU只可能运行于ARM模式、不可运行于Thumb或Thumb-2模式，所以它必须使用32位的指令；另一方面，64位平台的寄存器数量也翻了一翻，拥有了64个X-字头的寄存器。当然，程序还可以通过W-字头的名称直接访问寄存器的低32位空间。

上述程序的STP(Store Pair)指令把两个寄存器（即X29和X30）的值存储到栈里。虽然这个指令实际上可以把这对数值存储到内存中的做任意地址，但是由于该指令明确了SP寄存器，所以它就是通过栈来存储这对数值。ARM64平台的寄存器都是64位寄存器，每个寄存器可存储8字节数据，所以程序要分配16字节的空间来存储两个寄存器的值。 这条指令中的感叹号标志，意味着其标注的运算会被优先执行。即，该指令先把SP的值减去16，在此之后再把两个寄存器的值写在栈里。这属于预索引（pre-index）指令。此外还有延迟索引（post-index）指令与之对应。两者的区别，我们会在以后的章节中说明。 以更为易懂的x86指令来解读的话，这条指令相当于PUSH X29和PUSH X30两条指令。在ARM64平台上，X29寄存器是帧指针FP，X30起着LR的作用。所以这两个寄存器在函数的序言和尾声处成对出现。

第二条指令把 SP 的值复制给 X29，即 FP。这用来设置函数的栈帧。

ADRP 和 ADD 指令相互配合，把“Hello!”字符串的指针传递给 X0 寄存器，继而充当函数参数传递给被调用函数。受到指令方面的限制，ARM 无法通过单条指令就把一个较大的立即数赋值给寄存器。所以，编译器要组合使用数条指令进行立即数赋值。第一条 ADRP 把 4KB 页面的 地址传递给 X0，而后第二条 ADD 进行加法运算并给出最终的指针地址。在以后的章节中我们会详细讨论。 0x400000 + 0x648 = 0x400648。这个数是位于.rodata 数据段的 C 字符串“Hello!”的地址。

接下来，程序使用 BL 指令调用 puts()函数。

MOV 指令用来给 W0 寄存器置零。W0 是 X0 寄存器的低 32 位。main()函数通过 X0 寄存器来传递函数返回值 0。

在此之后，LDP (Load Pair)指令还原 X29 和 X30 寄存器的值。此处的这条指令没有感叹号标记，这意 味着它将率先进行赋值操作，而后再把 SP 的值与 16 进行求和运算。这属于延时索引(post-index)指令。

RET指令是 ARM64 平台的特色指令。虽然它的作用与 BX LR 相同，但是它实际上是按照寄存器的名 称进行跳转的(默认使用 X30 寄存器指向的地址)，通过底层指令提示 CPU 此处为函数的返回指令、不属 于普通转移指令的返回过程。RET 指令经过了面向硬件的优化处理，它的执行效率较高。
MIPS

在 MIPS 指令里，寄存器有两种命名方式。一种是以数字命名($0~$31)，另一种则是以伪名称(pseudoname)命名($V0~VA0，依此类推)。在 GCC 编译器生成的汇编指令中，寄存器都采用数字方式命名。 存储函数返回值的寄存器都是$2(即$V0)。 LI指令 是英文词组“Load Immediate(加载立即数)”的缩写。 J和JR指令都属于跳转指令，它们把执行流递交给调用者函数，跳转到$31即$RA 寄存器中的地址。这个寄存器相当于的 ARM 平台的 LR 寄存器。 分支(转移)指令延迟槽 (Branch delay slot)的现象。简单地说，不管分支(转移)发生与否，位于分支指 令后面的一条指令(在延时槽里的指令)，总是被先于分支指令提交。这是 RISC 精简指令集的一种特例， 我们不必在此处深究。总之，转移指令后面的这条赋值指令实际上是在转移指令之前运行的。
全局指针 Global Pointer

全局指针是 MIPS 软件系统的一个重要概念。们已经知道，每条 MIPS 指令都是 32 位的指令，所以 单条指令无法容纳 32 位地址(指针)。这种情况下 MIPS 就得传递一对指令才能使用一个完整的指针。 从另一方面来说，单条指令确实可以容纳一组由寄存器的符号、有符号的 16 位偏移量(有符号数)。 因此任何一条指令都可以构成的表达式，访问某个取值范围为“寄存器−32768”~“寄存器+32767”之 间的地址(总共 69KB)。为了简化静态数据的访问操作，MIPS 平台特地为此保留了一个专用的寄存器， 并且把常用数据分配到了一个大小为 64KB 的内存数据空间里。这种专用的寄存器就叫作“全局指针”寄存器。它的值是一个指针，指向 64KB(静态)数据空间的正中间。而这 64KB 空间通常用于存储全局变量，以及 printf()这类由外部导入的的外部函数地址。GCC 的开发团队认为:获取函数地址这类的操作，应 当由单条指令完成;双指令取址的运行效率不可接受。 在 ELF 格式的文件中，这个 64KB 的静态数据位于.sbss 和.sdata 之中。“.sbss”是 small BSS(Block Started by Symbol)的缩写，用于存储非初始化的数据。“.sdata”是 small data 的缩写，用于存储有初始化数值的数据。 根据这种数据布局编程人员可以自行决定把需要快速访问的数据放在.sdata、还是.sbss 数据段中。 有多年工作经验的人员可能会把全局指针和 MS-DOS 内存(参见本书第 49 章)、或者 MS-DOS 的 XMS/EMS 内存管理器联系起来。这些内存管理方式都把数据的内存存储空间划分为数个 64KB 区间。 全局指针并不是 MIPS 平台的专有概念。至少 PowerPC 平台也使用了这一概念。
Optimizing GCC
Optimizing GCC 4.4.5 汇编输出

$LC0:
; \000 is zero byte in octal base:
		.ascii "Hello, world!\012\000"
main:
; function prologue
;set tht GP:
		lui 		$28,$hi(__gnu_local_gp)
		addiu	$sp,$sp,-32
		addiu $28,$28,%lo(__gnu_local_gp)
; save the RA to the local stack:
		sw $31,28($sp)
; load the address of the puts() function from the GP to $25
		lw		$25,%call6(puts)($28)
; load the address of the text string to $4 ($a0):
		lui		$4,$hi($LC0)
; jump to puts(), saving the return address in the link register:
		jalr		$25
		addiu		$4,$4,%lo($LC0)		; branch delay slot
; restore thr RA:
		lw		$31,28($sp)
; copy 0 from $zero to $v:
		move $2,$0
; return by jumping to the RA:
		j		$31
; function epilogue:
		addiu $sp,$sp,32		; branch delay slot

主函数序言启动部分的指令初始化了全局指针寄存器GP寄存器的值，并且把它指向 64KB数据段的正中央。 同时，程序把RA寄存器的值存储于本地数据栈。 它同样使用puts()函数替代了printf()函数。而puts()函数的地址， 则通过LW(Load Word)指令加载到了$25 寄存器。 此后，字符串的高 16 位地址和低 16 位地址分别由LUI(Load Upper Immediate)和ADDIU(Add Immediate Unsigned Word)两条指令加载到$4 寄存器。LUI中的Upper一词说明它将数据存 储于寄存器的高 16 位。与此相对应，ADDIU则把操作符地址处的低 16 位进行了求和运算。ADDIU指令位于JALR 指令之后，但是会先于后者运行。$4 寄存器其实就是$A0 寄存器，在调用函数时传递第一个参数。 JALR (Jump and Link Register)指令跳转到$25 寄存器中的地址，即 puts()函数的启动地址，并且把下一 条 LW 指令的地址存储于 RA 寄存器。 可见，MIPS 系统调用函数的方法与 ARM 系统相似。需要注意的是， 由于分支延迟槽效应，存储于 RA 寄存器的值并非是已经运行过的、“下一条”指令的地址，而是更后面那 条(延迟槽之后的)指令的地址。所以，在执行这条 JALR 指令的时候，写入 RA 寄存器的值是 PC+8，即 ADDIU 后面的那条 LW 指令的地址。 第 19 行的 LW (Load Word)指令，用于把本地栈中的 RA 值恢复回来。请注意，这条指令并不位于被调用函数的函数尾声。 第 22 行的MOVE指令把$0($ZERO)的值复制给$2($V0)。MIPS 有一个常量寄存器，它里面的值是常量 0。使用$0 寄存器提供数值0。另外一个值得注意的现象:在 MIPS 系统之中，没有在寄 存器之间复制数值的(硬件)指令。确切地说，MOVE DST, SRC是通过加法指令ADD DST,SRC, $ZERO变相实现的，即DST=SRC+0，这两种操作等效。由此可见，MIPS 研发人员希望尽可能地复用 opcode，从 而精简 opcode 的总数。然而这并不代表每次运行 MOVE 指令时 CPU 都会进行实际意义上的加法运算。CPU 能够对这类伪指令进行优化处理，在运行它们的时候并不会用到 ALU(Arithmetic logic unit)。 第 24 行的J指令会跳转到 RA 所指向的地址，完成从被调用函数返回调用者函数的操作。还是由于分支延迟槽效应，其后的ADDIU指令会先于J指令运行，构成函数尾声。
Opimizing GCC4.4.5(IDA)

再来看看 IDA 生成的指令清单，熟悉一下各寄存器的伪名称。

.text:00000000  main:
.text:00000000
.text:00000000  var_10   = -0x10 
.text:00000000  var_4    = -4
.text:00000000
; function prologue. 
; set the GP:
.text:00000000  lui $gp, (__gnu_local_gp >> 16)
.text:00000004  addiu $sp, -0x20
.text:00000008 la  $gp, (__gnu_local_gp & 0xFFFF)
; save the RA to the local stack:
.text:0000000C sw  $ra, 0x20+var_4($sp)
; save the GP to the local stack:
; for some reason, this instruction is missing in the GCC assembly output: 
.text:00000010 sw $gp, 0x20+var_10($sp)
; load the address of the puts() function from the GP to $t9:
.text:00000014 lw  $t9,(puts & 0xFFFF)($gp)
; form the address of the text string in $a0:
.text:00000018 lui $a0, ($LC0 >> 16) # "Hello, world!" 
; jump to puts(), saving the return address in the link register:
.text:0000001C jalr  $t9
.text:00000020 la    $a0, ($LC0 & 0xFFFF) # "Hello, world!"
;restore the RA:
.text:00000024 lw    $ra, 0x20+var_4($sp)
; copy 0 from $zero to $v0:
.text:00000028 move  $v0, $zero
; return by jumping to the RA:
.text:0000002C jr    $ra
; function epilogue:
.text:00000030 addiu $sp, 0x20

程序中保存 puts()函数地址的寄存器叫作$T9 寄存器。这类 T-开头的寄存器叫作“临时”寄存器，用于保存代码里的临时值。调用者函数负责保存这些寄存器的数值(caller-saved)，因为它有可能会被被调用的函数重写。
Non-pimizing GCC 汇编输出

$LC0:
		.ascii "Hello, world!\012\000"
main:
; function prologue
; save the RA ($31) and FP in the stack:
		addiu	$sp,$sp,-32
		sw		$31,28($sp)
		sw		$fp,24($sp)
; set the FP (stack frame pointer):
		move	$fp,$sp
; set the GP:
		lui		$28,%hi(__gnu_local_gp)
		addiu	$28,$28,%lo(__gnu_local_gp)
; load the address of the text string:
		lui		$2,%hi($LC0)
		addiu	$4,$2,%lo($LC0)
; load the address of the puts() using GP:
		lw		$2,%call16(puts)($28)
		nop
; call puts():
		move	$25,$2
		jalr		$25
		nop					; branch delay slot
; restore the GP from the local stack:
		lw		$28,16($fp)
; set register $2 ($V0) to zero:
		move	$2,$0
; function epilogue. 
; restore the SP:
		move	$sp,$fp
; restore the RA:
		lw		$31,28($sp) 
; restore the FP:
		lw		$fp,24($sp)
		addiu	$sp,$sp,32 
; jump to the RA:
		j			$31
		nop					; branch delay slot

未经优化处理的 GCC 输出要详细得多。此处，我们可以观察到程序把FP当作栈帧的指针来用，而且它还有3个 NOP(空操作)指令。在这3个空操作指令中，第二个、第三个指令都位于分支跳转指令之后。 笔者个人认为(虽然目前无法肯定)，由于这些地方都存在分支延迟槽，所以 GCC 编译器会在分支语句之后都添加NOP指令。不过，在启用它的优化选项之后，GCC 可能就会删除这些 NOP 指令。所以，此 处仍然存在这些 NOP 指令。
Non-pimizing GCC (IDA)

.text:00000000 main: 
.text:00000000
.text:00000000 var_10 = -0x10
.text:00000000 var_8 = -8
.text:00000000 var_4 = -4
.text:00000000
; function prologue.
; save the RA and FP in the stack: 
.text:00000000 addiu  $sp, -0x20
.text:00000004 sw    $ra, 0x20+var_4($sp)
.text:00000008 sw    $fp, 0x20+var_8($sp)
; set the FP (stack frame pointer):
.text:0000000C move  $fp, $sp
; set the GP:
.text:00000010 la    $gp, __gnu_local_gp
.text:00000018 sw    $gp, 0x20+var_10($sp)
; load the address of the text string:å
.text:0000001C lui   $v0, (aHelloWorld >> 16) # "Hello, world!"
.text:00000020 addiu $a0, $v0, (aHelloWorld & 0xFFFF) # "Hello, world!" 
; load the address of puts() using the GP:
.text:00000024 lw    $v0, (puts & 0xFFFF)($gp)
.text:00000028 or    $at, $zero ; NOP
; call puts():
.text:0000002C move  $t9, $v0
.text:00000030 jalr  $t9
.text:00000034 or    $at, $zero ; NOP
; restore the GP from local stack: 
.text:00000038 lw    $gp, 0x20+var_10($fp)
; set register $2 ($V0) to zero:
.text:0000003C move  $v0, $zero
; function epilogue. 
; restore the SP:
.text:00000040 move  $sp, $fp
; restore the RA:
.text:00000044 lw    $ra, 0x20+var_4($sp)
; restore the FP:
.text:00000048 lw    $fp, 0x20+var_8($sp)
.text:0000004C addiu $sp, 0x20
; jump to the RA:
.text:00000050 jr    $ra
.text:00000054 or    $at, $zero ; NOP

在程序的第 15 行出现了一个比较有意思的现象——IDA 识别出了 LUI/ADDIU 指令对，把它们显示为 单条的伪指令LA(Load address)。那条伪指令占用了8个字节!这种伪指令(即“宏”)并非真正的MIPS 指令。通过这种名称替换,IDA 帮助我们这对指令的作用望文思义。 NOP 的显示方法也构成了它的另外一种特点。因为 IDA 并不会自动地把实际指令匹配为 NOP 指令， 所以位于第 22 行、第 26 行、第 41 行的指令都是OR $AT, $ZERO。表面上看，它将保留寄存器$AT 的 值与 0 进行或运算。但是从本质上讲，这就是发送给 CPU 的 NOP 指令。MIPS 和其他的一些硬件平台的 指令集都没有单独的 NOP 指令。
栈帧

本例使用寄存器来传递文本字符串的地址。但是它同时设置了局部栈，这是为什么呢?由于程序在调用 printf()函数的时候由于程序必须保存 RA 寄存器的值和 GP 的值，故而此处出现了数据栈。如果此函数是叶函数，它有可能不会出现函数的序言和尾声。
GDB分析
编译

使用如下命令编译我们的文件 gcc hello.c -O3 -o hello 将会在同目录下生成hello文件。
使用GDB调试

    执行命令：gdb hello开始调试。
    执行命令：b main，在main函数处下断点。
    执行命令：run，运行程序，程序会在断点处停止。
    执行命令：set step-mode on，开启step-mode 模式。
    执行命令：disas查看当前函数的反汇编指令。
    执行命令：s，执行一条汇编指令。
    执行命令：x/s address，查看某地址处的数据，并以s(tring)格式显示。

	root@kali:~/Desktop# gdb hw
	GNU gdb (Debian 7.12-6+b1) 7.12.0.20161007-git
	Copyright (C) 2016 Free Software Foundation, Inc.
	License GPLv3+: GNU GPL version 3 or later <http://gnu.org/licenses/gpl.html>
	This is free software: you are free to change and redistribute it.
	There is NO WARRANTY, to the extent permitted by law.  Type "show copying"
	and "show warranty" for details.
	This GDB was configured as "x86_64-linux-gnu".
	Type "show configuration" for configuration details.
	For bug reporting instructions, please see:
	<http://www.gnu.org/software/gdb/bugs/>.
	Find the GDB manual and other documentation resources online at:
	<http://www.gnu.org/software/gdb/documentation/>.
	For help, type "help".
	Type "apropos word" to search for commands related to "word"...
	Reading symbols from hw...(no debugging symbols found)...done.
	(gdb) b main
	Breakpoint 1 at 0x530
	(gdb) r
	Starting program: /root/Desktop/hw 

	Breakpoint 1, 0x0000555555554530 in main ()
	(gdb) set step-mode on
	(gdb) disas
	Dump of assembler code for function main:
	=> 0x0000555555554530 <+0>:	lea    0x1ad(%rip),%rdi        # 0x5555555546e4
	   0x0000555555554537 <+7>:	sub    $0x8,%rsp
	   0x000055555555453b <+11>:	callq  0x555555554510 <puts@plt>
	   0x0000555555554540 <+16>:	xor    %eax,%eax
	   0x0000555555554542 <+18>:	add    $0x8,%rsp
	   0x0000555555554546 <+22>:	retq   
	End of assembler dump.
	(gdb) s
	0x0000555555554537 in main ()
	(gdb) disas
	Dump of assembler code for function main:
	   0x0000555555554530 <+0>:	lea    0x1ad(%rip),%rdi        # 0x5555555546e4
	=> 0x0000555555554537 <+7>:	sub    $0x8,%rsp
	   0x000055555555453b <+11>:	callq  0x555555554510 <puts@plt>
	   0x0000555555554540 <+16>:	xor    %eax,%eax
	   0x0000555555554542 <+18>:	add    $0x8,%rsp
	   0x0000555555554546 <+22>:	retq   
	End of assembler dump.
	(gdb) x/s 0x5555555546e4
	0x5555555546e4:	"Hello, world"
	(gdb) 

#使用radare2分析

    查看程序中的字符串：rabin2 -z hello;
    使用r2命令分析：

root@kali:~/Desktop# r2 hw
[0x00000550]> aaa
[x] Analyze all flags starting with sym. and entry0 (aa)
[x] Analyze len bytes of instructions for references (aar)
[x] Analyze function calls (aac)
[x] Use -AA or aaaa to perform additional experimental analysis.
[x] Constructing a function name for fcn.* and sym.func.* functions (aan)
[0x00000550]> s main
[0x00000530]> pdf
            ;-- main:
            ;-- section..text:
/ (fcn) sym.main 23
|   sym.main ();
|              ; DATA XREF from 0x0000056d (entry0)
|           0x00000530      488d3dad0100.  lea rdi, qword str.Hello__world ; 0x6e4 ; "Hello, world" ; section 14 va=0x00000530 pa=0x00000530 sz=418 vsz=418 rwx=--r-x .text
|           0x00000537      4883ec08       sub rsp, 8
|           0x0000053b      e8d0ffffff     call sym.imp.puts           ; int puts(const char *s)
|           0x00000540      31c0           xor eax, eax
|           0x00000542      4883c408       add rsp, 8
\           0x00000546      c3             ret
[0x00000530]> ps @ 0x6e4
Hello, world
[0x00000530]> 

###作业
破解crackme
使用IDA打开文件，打开pseudocode窗口查看原函数
可知密码为250382
输入后程序闪退

